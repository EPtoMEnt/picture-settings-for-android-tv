plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.alexrcq.tvpicturesettings'

    defaultConfig {
        applicationId "com.alexrcq.tvpicturesettings"
        minSdkVersion 26
        compileSdk 32
        targetSdkVersion 32
        versionCode 34
        versionName "1.0.75"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "tvpicturesettings${defaultConfig.versionName}.apk"
        }
    }

    task createDebugKeystoreFromEnvironment {
        doLast {
            def keystoreBase64 = System.getenv("DEBUG_KEYSTORE_BASE64")
            if (keystoreBase64 != null) {
                def keystoreBytes = Base64.getDecoder().decode(keystoreBase64)
                def keystoreFile = file("$System.env.HOME/.android/debug.keystore")
                def outputStream = new FileOutputStream(keystoreFile)
                outputStream.write(keystoreBytes)
                outputStream.close()
            } else {
                println("DEBUG_KEYSTORE_BASE64 is not set. Creation cancelled.")
            }
        }
    }

    preBuild.dependsOn createDebugKeystoreFromEnvironment

    lintOptions {
        disable 'UnspecifiedRegisterReceiverFlag'
    }
}

dependencies {
    def leanback_version = '1.2.0-alpha02'
    implementation("androidx.leanback:leanback:$leanback_version")
    implementation("androidx.leanback:leanback-preference:$leanback_version")

    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation 'com.tananaev:adblib:1.3'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
}