plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'com.alexrcq.tvpicturesettings'
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.alexrcq.tvpicturesettings"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 21
        versionName "1.0.67"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debugMinified {
            initWith(buildTypes.debug)
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    buildToolsVersion '32.0.0'

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "tvpicturesettings${defaultConfig.versionName}.apk"
        }
    }
}

dependencies {
    def leanback_version = '1.2.0-alpha02'
    implementation("androidx.leanback:leanback:$leanback_version")
    implementation("androidx.leanback:leanback-preference:$leanback_version") {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation ("androidx.preference:preference-ktx:1.2.0") {
        exclude group: 'androidx.lifecycle', module:'lifecycle-viewmodel-ktx'
    }
    implementation 'com.tananaev:adblib:1.3'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation ("com.google.dagger:hilt-android:2.44")
    kapt "com.google.dagger:hilt-compiler:2.44"
}

kapt {
    correctErrorTypes true
}